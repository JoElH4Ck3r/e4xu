<?xml version="1.0"?>
<mx:WindowedApplication 
        xmlns:mx="http://www.adobe.com/2006/mxml"
        applicationComplete="init()"
		width="390"
		height="400"
        >
        <mx:Script>
                <![CDATA[
					import flash.display.Graphics;
					import flash.display.Loader;
					import flash.display.LoaderInfo;
					import flash.display.MovieClip;
					import flash.display.Sprite;
					import flash.events.Event;
					import flash.filesystem.File;
					import flash.filesystem.FileMode;
					import flash.filesystem.FileStream;
					import flash.media.Sound;
					import flash.net.FileFilter;
					import flash.system.LoaderContext;
					import flash.utils.ByteArray;
					import flash.utils.Endian;
					import org.wvxvws.encoding.SWFCompiler;
					
					private var _flv:ByteArray;
					private var _location:String;
					private var _swf:ByteArray;
					
					private function init():void
					{
							compileButton.enabled = false;
							testleButton.enabled = false;
							videoPlaceHolder.addChild(drawRect());
					}
					
					private function selectFLV():void
					{
							var file:File = new File();
							var asFilter:FileFilter = new FileFilter("Video FLV", "*.flv");
							try 
							{
									file.browseForOpen("Open", [asFilter]);
									file.addEventListener(Event.SELECT, fileSelected);
							}
							catch (error:Error)
							{
									trace("Failed:", error.message);
							}
					}
					
					private function fileSelected(event:Event):void 
					{
							var stream:FileStream = new FileStream();
							var fName:String = (event.target as File).name.replace(/\..+$/g, "").replace(/\W/g, "_");
							_flv = new ByteArray();
							_flv.endian = Endian.LITTLE_ENDIAN;
							stream.open(event.target as File, FileMode.READ);
							stream.readBytes(_flv, 0, stream.bytesAvailable);
							_location = (event.target as File).nativePath;
							_flv = SWFCompiler.compileEmbeddedVideo(_flv, fName);
							compileButton.enabled = true;
							testleButton.enabled = true;
					}
					
					private function testSWF():void
					{
							var loader:Loader = new Loader();
							loader.contentLoaderInfo.addEventListener(Event.COMPLETE, completeHandler);
							var context:LoaderContext = new LoaderContext(false, loaderInfo.applicationDomain);
							context.allowLoadBytesCodeExecution = true;
							loader.loadBytes(_flv, context);
							trace("loadbytes called");
							videoPlaceHolder.addChild(loader);
					}
					
					private function completeHandler(event:Event):void 
					{
							trace("complete handler");
							(event.target as LoaderInfo).removeEventListener(Event.COMPLETE, completeHandler);
							var loader:Loader = (event.target as LoaderInfo).loader;
							(loader.content as MovieClip).play();
					}
					
					private function compileSWF():void
					{
							trace(_location.replace(/\.(flv)$/i, ".swf"));
							var file:File = new File(_location.replace(/\.(flv)$/i, ".swf"));
							_flv.position = 0;
							trace(_flv.length);
							file.save(_flv);
							
					}
					
					private function drawRect():Sprite
					{
						var s:Sprite = new Sprite();
						var g:Graphics = s.graphics;
						g.beginFill(0x808080);
						g.drawRect(0, 0, 320, 240);
						g.endFill();
						return s;
					}
                ]]>
        </mx:Script>
        <mx:Button 
                id="selectButton"
                label="Select FLV"
                click="selectFLV()"
                />
        <mx:Button 
                id="compileButton"
                label="Compile SWF"
                click="compileSWF()"
                />
        <mx:Button 
                id="testleButton"
                label="Test"
                click="testSWF()"
                />
		<mx:UIComponent 
			id="videoPlaceHolder" 
			width="320"
			height="240"
			/>
</mx:WindowedApplication>